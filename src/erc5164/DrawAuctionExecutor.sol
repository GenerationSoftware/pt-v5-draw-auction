// SPDX-License-Identifier: GPL-3.0
pragma solidity 0.8.17;

import { ExecutorAware } from "src/abstract/ExecutorAware.sol";
import { Phase } from "src/abstract/PhaseManager.sol";
import { IDrawAuction } from "src/interfaces/IDrawAuction.sol";

contract DrawAuctionExecutor is ExecutorAware {
  /* ============ Events ============ */

  /**
   * @notice Emitted when the DrawAuctionDispatcher has been set.
   * @param drawAuctionDispatcher Address of the DrawAuctionDispatcher
   */
  event DrawAuctionDispatcherSet(address drawAuctionDispatcher);

  /* ============ Custom Errors ============ */

  /// @notice Thrown when the originChainId passed to the constructor is zero.
  error OriginChainIdZero();

  /// @notice Thrown when the DrawAuctionDispatcher address passed to the constructor is zero address.
  error DrawAuctionDispatcherZeroAddress();

  /// @notice Thrown if the DrawAuctionDispatcher has already been set.
  error DrawAuctionDispatcherAlreadySet();

  /// @notice Thrown when the PrizePool address passed to the constructor is zero address.
  error DrawAuctionZeroAddress();

  /// @notice Thrown when the message was dispatched from an unsupported chain ID.
  error L1ChainIdUnsupported(uint256 fromChainId);

  /// @notice Thrown when the message was not executed by the executor.
  error L2SenderNotExecutor(address sender);

  /// @notice Thrown when the message was not dispatched by the DrawAuctionDispatcher on the origin chain.
  error L1SenderNotDispatcher(address sender);

  /* ============ Variables ============ */

  /// @notice ID of the origin chain that dispatches the auction phases and random number.
  uint256 internal immutable _originChainId;

  /// @notice Address of the DrawAuctionDispatcher on the origin chain that dispatches the auction phases and random number.
  address internal _drawAuctionDispatcher;

  /// @notice Address of the DrawAuction on L2 to complete
  IDrawAuction internal immutable _drawAuction;

  /* ============ Constructor ============ */

  /**
   * @notice DrawAuctionExecutor constructor.
   * @param originChainId_ ID of the origin chain
   * @param _executor Address of the ERC-5164 contract that executes the bridged calls
   * @param drawAuction_ Address of the Draw Auction to push to
   */
  constructor(
    uint256 originChainId_,
    address _executor,
    IDrawAuction drawAuction_
  ) ExecutorAware(_executor) {
    if (originChainId_ == 0) revert OriginChainIdZero();
    if (address(drawAuction_) == address(0)) revert DrawAuctionZeroAddress();

    _originChainId = originChainId_;
    _drawAuction = drawAuction_;
  }

  /* ============ External Functions ============ */

  /**
   * @notice Complete the auction and current draw.
   * @param _auctionPhases Array of auction phases
   * @param _randomNumber Random number generated by the RNG service on the origin chain
   */
  function completeAuction(Phase[] memory _auctionPhases, uint256 _randomNumber) public {
    _checkSender();
    _drawAuction.completeAuction(_auctionPhases, _randomNumber);
  }

  /* ============ Getter Functions ============ */

  /**
   * @notice Get the ID of the origin chain.
   * @return ID of the origin chain
   */
  function originChainId() public view returns (uint256) {
    return _originChainId;
  }

  /**
   * @notice Get the address of the DrawAuctionDispatcher on the origin chain.
   * @return Address of the DrawAuctionDispatcher on the origin chain
   */
  function drawAuctionDispatcher() public view returns (address) {
    return _drawAuctionDispatcher;
  }

  /**
   * @notice Get the address of the IDrawAuction that this executor calls
   * @return IDrawAuction that this executor will call
   */
  function drawAuction() public view returns (IDrawAuction) {
    return _drawAuction;
  }

  /* ============ Setters ============ */

  /**
   * @notice Set the DrawAuctionDispatcher address.
   * @dev Can only be called once.
   *      If the transaction get front-run at deployment, we can always re-deploy the contract.
   */
  function setDrawAuctionDispatcher(address drawAuctionDispatcher_) public {
    if (_drawAuctionDispatcher != address(0)) revert DrawAuctionDispatcherAlreadySet();
    if (drawAuctionDispatcher_ == address(0)) revert DrawAuctionDispatcherZeroAddress();

    _drawAuctionDispatcher = drawAuctionDispatcher_;

    emit DrawAuctionDispatcherSet(drawAuctionDispatcher_);
  }

  /* ============ Internal Functions ============ */

  /**
   * @notice Checks that:
   *          - the call has been dispatched from the supported chain
   *          - the sender on the receiving chain is the executor
   *          - the sender on the origin chain is the DrawDispatcher
   */
  function _checkSender() internal view {
    if (_fromChainId() != _originChainId) revert L1ChainIdUnsupported(_fromChainId());
    if (!isTrustedExecutor(msg.sender)) revert L2SenderNotExecutor(msg.sender);
    if (_msgSender() != address(_drawAuctionDispatcher)) revert L1SenderNotDispatcher(_msgSender());
  }
}
